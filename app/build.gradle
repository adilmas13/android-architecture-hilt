apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'androidx.navigation.safeargs'

apply from: '../project_settings.gradle'

apply from: "../ktlint.gradle"

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion
    defaultConfig {
        applicationId project.applicationId
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode project.versionCode
        versionName project.versionName
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        resConfigs "en" //including only english resource files - done to reduce apk size
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "Android-Architecture-debug_v${versionName}.apk"
                }
            }
        }
        releaseDebug {
            debuggable true
            applicationIdSuffix ".releaseDebug"
//            signingConfig signingConfigs.config
        }
        release {
            debuggable project.enableDebugLogs
            shrinkResources project.enableCodeShrinking
            minifyEnabled project.enableProguard
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "Android-Architecture_v${versionName}.apk"
                }
            }
        }
    }
    compileOptions { // adding Java 8 bytecode since Coil requires it
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation project(path: ':data')
    implementation project(path: ':domain')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation kotlinDependencies.values()
    implementation androidDependencies.values()
    implementation imageLoaderDependencies.values()
    implementation otherDependencies.values()
    annotationProcessor annotationProcessorDependencies.values()
    testImplementation unitTestDependencies.values()
    androidTestImplementation uiTestDependencies.values()

}
