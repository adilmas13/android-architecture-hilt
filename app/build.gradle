apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply from: '../project_settings.gradle'
apply from: "../ktlint.gradle"
apply from: "../dokka.gradle"

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion
    defaultConfig {
        applicationId project.applicationId
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode project.versionCode
        versionName project.versionName
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        resConfigs "en" //including only english resource files - done to reduce apk size
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix '-dev'
            buildConfigField("String", 'BASE_URL', '"https://reqres.in/"')
        }
        releaseDebug {
            debuggable true
            shrinkResources project.enableCodeShrinking
            minifyEnabled project.enableProguard
            signingConfig signingConfigs.config
            buildConfigField("String", 'BASE_URL', '"https://reqres.in/"')
        }
        release {
            debuggable project.enableDebugLogs
            shrinkResources project.enableCodeShrinking
            minifyEnabled project.enableProguard
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
            buildConfigField("String", 'BASE_URL', '"https://reqres.in/"')
        }
    }
    compileOptions { // adding Java 8 bytecode since Coil, Hilt requires it
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':data')
    implementation project(path: ':domain')
    implementation appDependencies.values()
    kapt annotationProcessingDependencies.values()
    testImplementation unitTestDependencies.values()
    androidTestImplementation uiTestDependencies.values()
}
