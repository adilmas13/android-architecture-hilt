apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'androidx.navigation.safeargs'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply from: '../project_settings.gradle'
apply from: "../ktlint.gradle"
apply plugin: 'org.jetbrains.dokka'

tasks.dokkaHtml {
    outputDirectory = "$buildDir/docs"
}

android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion
    defaultConfig {
        applicationId project.applicationId
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        versionCode project.versionCode
        versionName project.versionName
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true
        resConfigs "en" //including only english resource files - done to reduce apk size
    }
    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            buildConfigField("String", 'BASE_URL', '"https://reqres.in/"')
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "Android-Architecture-debug_v${versionName}.apk"
                }
            }
        }
//        releaseDebug {
//            debuggable true
//            shrinkResources project.enableCodeShrinking
//            minifyEnabled project.enableProguard
//            buildConfigField("String", 'BASE_URL', '"https://reqres.in/"')
//        }
        release {
            debuggable project.enableDebugLogs
            shrinkResources project.enableCodeShrinking
            minifyEnabled project.enableProguard
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", 'BASE_URL', '"https://reqres.in/"')
            applicationVariants.all { variant ->
                variant.outputs.all {
                    outputFileName = "Android-Architecture_v${versionName}.apk"
                }
            }
        }
    }
    compileOptions { // adding Java 8 bytecode since Coil requires it
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(path: ':data')
    implementation project(path: ':domain')
    implementation appDependencies.values()
    kapt annotationProcessingDependencies.values()
    testImplementation unitTestDependencies.values()
    androidTestImplementation uiTestDependencies.values()
}
